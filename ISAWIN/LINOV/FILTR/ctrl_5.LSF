#combinatory
(*Вимикання стейт-машини*)
if NOT Amr_5_ OR Alarm_5_  OR NOT Dio_5_ then 
	State_5_:=ST_OFF;
end_if;


(*Вмикання стейт-машини*)
(*if redge(Amr_5_,re_Amr) then 
	State_5_:=ST_START;
end_if;*)

IF State_5_>ST_START then (*Рахуємо час роботи фільтра*)
	if Tfckl then 
		Tf_5_:=Tf_5_+1;
	end_if;
	Tf_m_5_:=MOD(Tf_5_,60);
	Tf_h_5_:=Tf_5_/60;
else
	Tf_m_5_:=0;
	Tf_h_5_:=0;
end_if;

if State_5_ > ST_START AND NOT AM_5_ then  (*Якщо перемкнули на ручний режим*)
	Vl_5_1:=false;
	Vl_5_2:=false;
	Vl_5_3:=false;
	State_5_:=ST_START; (*Положення ST_START окрім іншого використовується для ручного управління*)
end_if;


(*Головна стейт-машина управління фільтром*)
case State_5_ of

ST_OFF: (*Все вимкнути*)
	Vl_5_1:=false;
	Vl_5_2:=false;
	Vl_5_3:=false;
	Alm_5_6:=false; (*Зняти аварію останньої вигрузки*)

	res:=off_all(); (*Погасити всі Half і Full*)


	(*Am_5_:=false; вимикати автомат*)
	State_5_:=ST_START;

ST_START: (*Запуск фільтра в роботу*)

	if Tfr_5_ then (*Відслідкувати час роботи фільтра*)
		Tf_5_:=0;
	end_if;

if Am_5_ then 
	Full_5_:=false;		
	Half_5_:=false;

	Streg_5_:=false; (*Скинути команду регенерації*)
	Man_5_:=false; (*Зняти команду ручного запуску регенерації*)

	if NOT (Cl_5_2 OR Cl_5_3) then (*Якщо закриті клапани 2 та 3 тоді*)
		Vl_5_1:=true; (*Відкрити клапан 1*)
	end_if;

	if Cl_5_1 then  (*Необхідність даної умови сумнівна*)
		State_5_:=ST_FLT;
	end_if;
end_if;

ST_FLT: (*Стадія фільтрування*)
		Vl_5_1:=TRUE;
		(*тут треба вписати умову закінчення фільтрації*)
		if Streg_5_ OR Man_5_ then 
			State_5_:=ST_STREG;

			if r_5_ then (*правління індикацією на блоці*)
				Full_5_:=true;
			else
				HAlf_5_:=true;
			end_if;
		end_if;


St_STREG: (*Розпочати регенерацюю, закрити клапант 1 *)
	Vl_5_1:=false; (*Закрити клапан подачі на фільтр*)
	if not Cl_5_1	then (*Якщо заврався*)
		if Nc_zd=3 then (* якщо випуск іде через клапан вивантаження*)
			State_5_:=ST_V3;
			Ls_start:=L_sus; (*Зберегти початковий рівень*)
			Vl_5_3:=true; (*Відкрити клапан 3*)
		else
			State_5_:=ST_V2;
			Vl_5_2:=true; (*Відкрити клапан 2*)
		end_if;
	end_if;
ST_V2: (*Злив через кран 2*)
	if T_v2_5_ then (*Якщо час вийшов*)
		if r_5_ then (*Якщо із випуском осаду*)
			Vl_5_3:=true; (*Відкрити клапан 3*)
			Ls_start:=L_sus; (*Зберегти початковий рівень*)
			State_5_:=ST_V3;
		else (*інакше *)
			Vl_5_2:=false; (*все закрити*)
			Vl_5_3:=false;
			State_5_:=ST_START;
		end_if;
	end_if;
ST_V3: (*Злив через кран 3*)
	if T_v3_5_ then  (*Коли вийшов час*)
			Vl_5_2:=false; (*все закрити*)
			Vl_5_3:=false;
			Alm_5_6:=L_sus-Ls_start<d_L_sus_zd; (*Перевірити приріст рівня*)

			State_5_:=ST_START; (*Погнали по новій*)

	end_if;
else
	State_5_:=ST_OFF;

end_case;


