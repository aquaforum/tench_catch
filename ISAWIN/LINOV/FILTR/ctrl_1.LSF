#combinatory
(*Вимикання стейт-машини*)
if NOT Amr_1_ OR Alarm_1_  OR NOT Dio_1_ then 
	State_1_:=ST_OFF;
end_if;


(*Вмикання стейт-машини*)
(*if redge(Amr_1_,re_Amr) then 
	State_1_:=ST_START;
end_if;*)

IF State_1_>ST_START then (*Рахуємо час роботи фільтра*)
	if Tfckl then 
		Tf_1_:=Tf_1_+1;
	end_if;
	Tf_m_1_:=MOD(Tf_1_,60);
	Tf_h_1_:=Tf_1_/60;
else
	Tf_m_1_:=0;
	Tf_h_1_:=0;
end_if;

if State_1_ > ST_START AND NOT AM_1_ then  (*Якщо перемкнули на ручний режим*)
	Vl_1_1:=false;
	Vl_1_2:=false;
	Vl_1_3:=false;
	State_1_:=ST_START; (*Положення ST_START окрім іншого використовується для ручного управління*)
end_if;


(*Головна стейт-машина управління фільтром*)
case State_1_ of

ST_OFF: (*Все вимкнути*)
	Vl_1_1:=false;
	Vl_1_2:=false;
	Vl_1_3:=false;
	Alm_1_6:=false; (*Зняти аварію останньої вигрузки*)

	res:=off_all(); (*Погасити всі Half і Full*)

	(*Am_1_:=false; вимикати автомат*)
	State_1_:=ST_START;

ST_START: (*Запуск фільтра в роботу*)

	if Tfr_1_ then (*Відслідкувати час роботи фільтра*)
		Tf_1_:=0;
	end_if;

if Am_1_ then 
	Full_1_:=false;		
	Half_1_:=false;

	Streg_1_:=false; (*Скинути команду регенерації*)
	Man_1_:=false; (*Зняти команду ручного запуску регенерації*)

	if NOT (Cl_1_2 OR Cl_1_3) then (*Якщо закриті клапани 2 та 3 тоді*)
		Vl_1_1:=true; (*Відкрити клапан 1*)
	end_if;

	if Cl_1_1 then  (*Необхідність даної умови сумнівна*)
		State_1_:=ST_FLT;
	end_if;
end_if;

ST_FLT: (*Стадія фільтрування*)
		Vl_1_1:=TRUE;
		(*тут треба вписати умову закінчення фільтрації*)
		if Streg_1_ OR Man_1_ then 
			State_1_:=ST_STREG;

			if r_1_ then (*правління індикацією на блоці*)
				Full_1_:=true;
			else
				HAlf_1_:=true;
			end_if;
		end_if;


St_STREG: (*Розпочати регенерацюю, закрити клапант 1 *)
	Vl_1_1:=false; (*Закрити клапан подачі на фільтр*)
	if not Cl_1_1	then (*Якщо заврався*)

		if Nc_zd=3 then (* якщо випуск іде через клапан вивантаження*)
			State_1_:=ST_V3;
			Ls_start:=L_sus; (*Зберегти початковий рівень*)
			Vl_1_3:=true; (*Відкрити клапан 3*)
		else
			State_1_:=ST_V2;
			Vl_1_2:=true; (*Відкрити клапан 2*)
		end_if;
	end_if;
	
ST_V2: (*Злив через кран 2*)
	if T_v2_1_ then (*Якщо час вийшов*)
		if r_1_ then (*Якщо із випуском осаду*)
			Vl_1_3:=true; (*Відкрити клапан 3*)
			Ls_start:=L_sus; (*Зберегти початковий рівень*)
			State_1_:=ST_V3;
		else (*інакше *)
			Vl_1_2:=false; (*все закрити*)
			Vl_1_3:=false;
			State_1_:=ST_START;
		end_if;
	end_if;
ST_V3: (*Злив через кран 3*)
	if T_v3_1_ then  (*Коли вийшов час*)
			Vl_1_2:=false; (*все закрити*)
			Vl_1_3:=false;
			Alm_1_6:=L_sus-Ls_start<d_L_sus_zd; (*Перевірити приріст рівня*)

			State_1_:=ST_START; (*Погнали по новій*)
	end_if;
else
	State_1_:=ST_OFF;

end_case;


