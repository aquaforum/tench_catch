#combinatory
(*Вимикання стейт-машини*)
if NOT Amr_4_ OR Alarm_4_  OR NOT Dio_4_ then 
	State_4_:=ST_OFF;
end_if;


(*Вмикання стейт-машини*)
(*if redge(Amr_4_,re_Amr) then 
	State_4_:=ST_START;
end_if;*)

IF State_4_>ST_START then (*Рахуємо час роботи фільтра*)
	if Tfckl then 
		Tf_4_:=Tf_4_+1;
	end_if;
	Tf_m_4_:=MOD(Tf_4_,60);
	Tf_h_4_:=Tf_4_/60;
else
	Tf_m_4_:=0;
	Tf_h_4_:=0;
end_if;

if State_4_ > ST_START AND NOT AM_4_ then  (*Якщо перемкнули на ручний режим*)
	Vl_4_1:=false;
	Vl_4_2:=false;
	Vl_4_3:=false;
	State_4_:=ST_START; (*Положення ST_START окрім іншого використовується для ручного управління*)
end_if;


(*Головна стейт-машина управління фільтром*)
case State_4_ of

ST_OFF: (*Все вимкнути*)
	Vl_4_1:=false;
	Vl_4_2:=false;
	Vl_4_3:=false;
	Alm_4_6:=false; (*Зняти аварію останньої вигрузки*)

	res:=off_all(); (*Погасити всі Half і Full*)

	(*Am_4_:=false; вимикати автомат*)
	State_4_:=ST_START;

ST_START: (*Запуск фільтра в роботу*)
	if Tfr_4_ then (*Відслідкувати час роботи фільтра*)
		Tf_4_:=0;
	end_if;

if Am_4_ then 
	Full_4_:=false;		
	Half_4_:=false;

	Streg_4_:=false; (*Скинути команду регенерації*)
	Man_4_:=false; (*Зняти команду ручного запуску регенерації*)

	if NOT (Cl_4_2 OR Cl_4_3) then (*Якщо закриті клапани 2 та 3 тоді*)
		Vl_4_1:=true; (*Відкрити клапан 1*)
	end_if;

	if Cl_4_1 then  (*Необхідність даної умови сумнівна*)
		State_4_:=ST_FLT;
	end_if;
end_if;

ST_FLT: (*Стадія фільтрування*)
		Vl_4_1:=TRUE;
		(*тут треба вписати умову закінчення фільтрації*)
		if Streg_4_ OR Man_4_ then 
			State_4_:=ST_STREG;

			if r_4_ then (*правління індикацією на блоці*)
				Full_4_:=true;
			else
				HAlf_4_:=true;
			end_if;
		end_if;


St_STREG: (*Розпочати регенерацюю, закрити клапант 1 *)
	Vl_4_1:=false; (*Закрити клапан подачі на фільтр*)
	if not Cl_4_1	then (*Якщо заврався*)
		if Nc_zd=3 then (* якщо випуск іде через клапан вивантаження*)
			State_4_:=ST_V3;
			Ls_start:=L_sus; (*Зберегти початковий рівень*)
			Vl_4_3:=true; (*Відкрити клапан 3*)
		else
			State_4_:=ST_V2;
			Vl_4_2:=true; (*Відкрити клапан 2*)
		end_if;
	end_if;
ST_V2: (*Злив через кран 2*)
	if T_v2_4_ then (*Якщо час вийшов*)
		if r_4_ then (*Якщо із випуском осаду*)
			Vl_4_3:=true; (*Відкрити клапан 3*)
			Ls_start:=L_sus; (*Зберегти початковий рівень*)
			State_4_:=ST_V3;
		else (*інакше *)
			Vl_4_2:=false; (*все закрити*)
			Vl_4_3:=false;
			State_4_:=ST_START;
		end_if;
	end_if;
ST_V3: (*Злив через кран 3*)
	if T_v3_4_ then  (*Коли вийшов час*)
			Vl_4_2:=false; (*все закрити*)
			Vl_4_3:=false;
			Alm_4_6:=L_sus-Ls_start<d_L_sus_zd; (*Перевірити приріст рівня*)

			State_4_:=ST_START; (*Погнали по новій*)

	end_if;
else
	State_4_:=ST_OFF;

end_case;


