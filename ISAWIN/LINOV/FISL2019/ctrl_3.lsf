#combinatory
(*Вимикання стейт-машини*)
if NOT Amr_3_ OR Alarm_3_  OR NOT Dio_3_ then 
	State_3_:=ST_OFF;
end_if;


(*Вмикання стейт-машини*)
(*if redge(Amr_3_,re_Amr) then 
	State_3_:=ST_START;
end_if;*)

IF State_3_>ST_START then (*Рахуємо час роботи фільтра*)
	if Tfckl then 
		Tf_3_:=Tf_3_+1;
	end_if;
	Tf_m_3_:=MOD(Tf_3_,60);
	Tf_h_3_:=Tf_3_/60;
else
	Tf_m_3_:=0;
	Tf_h_3_:=0;
end_if;

if State_3_ > ST_START AND NOT AM_3_ then  (*Якщо перемкнули на ручний режим*)
	Vl_3_1:=false;
	Vl_3_2:=false;
	Vl_3_3:=false;
	State_3_:=ST_START; (*Положення ST_START окрім іншого використовується для ручного управління*)
end_if;


(*Головна стейт-машина управління фільтром*)
case State_3_ of

ST_OFF: (*Все вимкнути*)
	Vl_3_1:=false;
	Vl_3_2:=false;
	Vl_3_3:=false;
	Alm_3_6:=false; (*Зняти аварію останньої вигрузки*)

	res:=off_all(); (*Погасити всі Half і Full*)


	(*Am_3_:=false; вимикати автомат*)
	State_3_:=ST_START;

ST_START: (*Запуск фільтра в роботу*)

	if Tfr_3_ then (*Відслідкувати час роботи фільтра*)
		Tf_3_:=0;
	end_if;

if Am_3_ then 
	Full_3_:=false;		
	Half_3_:=false;

	Streg_3_:=false; (*Скинути команду регенерації*)
	Man_3_:=false; (*Зняти команду ручного запуску регенерації*)

	if NOT (Cl_3_2 OR Cl_3_3) then (*Якщо закриті клапани 2 та 3 тоді*)
		Vl_3_1:=true; (*Відкрити клапан 1*)
	end_if;

	if Cl_3_1 then  (*Необхідність даної умови сумнівна*)
		State_3_:=ST_FLT;
	end_if;
end_if;

ST_FLT: (*Стадія фільтрування*)
		Vl_3_1:=TRUE;
		(*тут треба вписати умову закінчення фільтрації*)
		if Streg_3_ OR Man_3_ then 
			State_3_:=ST_STREG;

			if r_3_ then (*правління індикацією на блоці*)
				Full_3_:=true;
			else
				HAlf_3_:=true;
			end_if;
		end_if;


St_STREG: (*Розпочати регенерацюю, закрити клапант 1 *)
	Vl_3_1:=false; (*Закрити клапан подачі на фільтр*)
	if not Cl_3_1	then (*Якщо заврався*)
		if Nc_zd=3 then (* якщо випуск іде через клапан вивантаження*)
			State_3_:=ST_V3;
			Ls_start:=L_sus; (*Зберегти початковий рівень*)
			Vl_3_3:=true; (*Відкрити клапан 3*)
		else
			State_3_:=ST_V2;
			Vl_3_2:=true; (*Відкрити клапан 2*)
		end_if;
	end_if;
ST_V2: (*Злив через кран 2*)
	if T_v2_3_ then (*Якщо час вийшов*)
		if r_3_ then (*Якщо із випуском осаду*)
			Vl_3_3:=true; (*Відкрити клапан 3*)
			Ls_start:=L_sus; (*Зберегти початковий рівень*)
			State_3_:=ST_V3;
		else (*інакше *)
			Vl_3_2:=false; (*все закрити*)
			Vl_3_3:=false;
			State_3_:=ST_START;
		end_if;
	end_if;
ST_V3: (*Злив через кран 3*)
	if T_v3_3_ then  (*Коли вийшов час*)
			Vl_3_2:=false; (*все закрити*)
			Vl_3_3:=false;
			Alm_3_6:=L_sus-Ls_start<d_L_sus_zd; (*Перевірити приріст рівня*)

			State_3_:=ST_START; (*Погнали по новій*)

	end_if;
else
	State_3_:=ST_OFF;

end_case;


